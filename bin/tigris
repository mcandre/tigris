#!/usr/bin/osascript

-- Usage: tigris [<frame count>]

-- Preconditions:
--
-- * Kindle app tiled to left half of full screen.
-- * Desired book open to starting page.

-- https://gist.github.com/andrewbonnington/c3cc79e4a0af5368c50f
on ceil(x)
	set y to x div 1

	if x > 0 and x mod 1 is not 0 then
		set y to y + 1
	end if

	return y
end ceil

on run argv
    set n to 1

    try
        set n to item 1 of argv as number
    end try

    set digits to ceil((do shell script "echo 'l(" & n & "+0.1)/l(10)' | bc -l") as real)
    set padding to "0"
    set inverse_pad_length to -1 * digits

    repeat (digits-1) times
        set padding to padding & "0"
    end repeat

    set kindle_application to "Kindle"
    set kindle_process to "Amazon Kindle"
    set target_dir to "~/Downloads/tigris"
    do shell script ("mkdir -p " & target_dir)

    tell application "System Events"
        tell application process kindle_process
            set frontmost to true

            set w to window 1
            set dimensions_pos to position of w

            --
            -- Work around Apple bug miscalculating height for half full screen tiled windows
            --
            set height to 0
            repeat with p in paragraphs of (do shell script "system_profiler SPDisplaysDataType | awk '/Resolution/ { print $4 }'")
                set height to word 1 of p as number
            end repeat

            set dimensions_size to {item 1 of (size of w as list), height}
        end tell
    end tell

    set rect to ("" & (item 1 of dimensions_pos) & "," & (item 2 of dimensions_pos) & "," & (item 1 of dimensions_size) & "," & (item 2 of dimensions_size))
    set i to 0

    repeat n times
        set frame_padded to text inverse_pad_length thru -1 of (padding & i)
        do shell script ("screencapture -R " & rect & " " & target_dir & "/" & frame_padded & ".png")
        set i to i + 1

        tell application "System Events"
            tell process kindle_process
                key code 124 -- right arrow
            end tell
        end tell

        delay 1
    end repeat
end run
